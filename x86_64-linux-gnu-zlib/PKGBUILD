# Maintainer: Blue DeviL // SCT <bluedevil.SCT@gmail.com>

_target=x86_64-linux-gnu
pkgbase=zlib
pkgname=($_target-zlib $_target-minizip)
epoch=1
pkgver=1.3.1
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP for x86_64 target'
arch=(any)
license=(Zlib)
url="https://www.zlib.net/"
options=(!buildflags staticlibs !emptydirs)
makedepends=('x86_64-linux-gnu-gcc')
source=(https://github.com/madler/zlib/releases/download/v$pkgver/$pkgbase-$pkgver.tar.xz{,.asc})
sha512sums=('1e8e70b362d64a233591906a1f50b59001db04ca14aaffad522198b04680be501736e7d536b4191e2f99767e7001ca486cd802362cca2be05d5d409b83ea732d'
            'SKIP')
b2sums=('42d109223801a493de6d52e7343403d7fc3234a6ca816425fe41ac9c18019b01b93841acd28a235e99f2256a6a17f93624e96b2ddb58d588c8190a6bedb82910'
        'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')  # Mark Adler <madler@alumni.caltech.edu>

prepare() {
  cd "${srcdir}"/zlib-${pkgver}
  
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export STRIP=${_target}-strip
  export AR=${_target}-ar

  cd contrib/minizip
  #sed -i "s|\-version\-info|\-no\-undefined \-version\-info|g" Makefile.am
  cp Makefile Makefile.orig
  cp ../README.contrib readme.txt
  autoreconf -fiv
}

build() {
  cd $pkgbase-$pkgver

  CFLAGS="${CFLAGS/ -Werror=format-security/ -ffat-lto-objects/}"
  CXXFLAGS="${CXXFLAGS/ -Werror=format-security/}"

  ./configure \
    --prefix=/usr/${_target} \
    --libdir=/usr/${_target}/lib \
    --includedir=/usr/${_target}/include \
    --enable-shared

  make $MAKEFLAGS

  cd contrib/minizip
  ./configure \
    --host=${CHOST} \
    --prefix=/usr/${_target} \
    --libdir=/usr/${_target}/lib \
    --includedir=/usr/${_target}/include \
    --enable-static=no

  make $MAKEFLAGS
}

# need qemu for test
#check() {
#  make test -C $pkgbase-$pkgver
#
#  make -f Makefile.orig test -C $pkgbase-$pkgver/contrib/minizip
#}

package_x86_64-linux-gnu-zlib() {
  depends=(${_target}-glibc)
  provides=(libz.so)

  make install DESTDIR="$pkgdir" -C $pkgbase-$pkgver
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/${_target}/share/licenses/$pkgname/"
}

package_x86_64-linux-gnu-minizip() {
  pkgdesc='Mini zip and unzip based on zlib'
  depends=(
    ${_target}-glibc
    ${_target}-zlib
  )

  make install DESTDIR="$pkgdir" -C $pkgbase-$pkgver/contrib/minizip
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/${_target}/share/licenses/$pkgname/"

  # https://github.com/madler/zlib/pull/229
  rm "$pkgdir/usr/${_target}/include/minizip/crypt.h"
}

# vim: ts=2 sw=2 et:
